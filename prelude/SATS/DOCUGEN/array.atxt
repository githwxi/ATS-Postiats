(* ****** ****** *)

%{
implement srcfilename_get () = "../array.sats"
%}

(* ****** ****** *)

%{
val () = patscode_count_reset()
val () = patscode_prefix_set("TRYIT/prelude_array")
%}

(* ****** ****** *)

#decltitle("\
ATSLIB/prelude/array\
")

(* ****** ****** *)

#declpreamble('\

#para('\

The type for a plain ungarnished array containing N elements of type T is
denoted by the special syntax #stacode("@[T][N]"). The size of this
array-type is N times the size of T and its linearity coincides with the
linearity of T, that is, the type #stacode("@[T][N]") is linear if and only
if T is linear.  It should be noted that a value of this form of array-type
can be passed to a function call only as a call-by-reference parameter
(unless certain special arrangement is made for it to be passed as a
call-by-value parameter).\

')#comment("para")

#para('\

Please see #myatscodelink("prelude/SATS/array.sats", "array.sats") and
#myatscodelink("prelude/DATS/array.dats", "array.dats")
(plus
#myatscodelink("prelude/DATS/array_bsearch.dats", "array_bsearch.dats"),
#myatscodelink("prelude/DATS/array_quicksort.dats", "array_quicksort.dats"))
for the SATS file
and DATS files in ATSLIB where the functions in this package are declared
and implemented.\

')

')#comment("declpreamble")

(* ****** ****** *)

#declname("array_v")
#declsynop2('\
#para('\
#pats2xhtmls("\
viewtypedef array_v (a: viewt@ype, l:addr, n: int) = @[a][n] @ l\
")\
')')
#decldescrpt('\

Given a type T, an address L and an integer N, the view
#stacode("array_v(T, L, N)") means that a value of the type
#stacode("@[T][N]") is stored at the location L. An equivalent
definition of #stacode("array_v") can be given as follows:

#pats2xhtmls("\
dataview array_v
  (a:vt@ype+, l:addr, int) =
  | array_v_nil (a, l, 0) of ((*none*))
  | {n:nat}
    array_v_cons (a, l, n+1) of (a @ l, array_v (a, l+sizeof(a), n))
// end of [array_v]
")

where #stacode("array_v") is (recursively) defined as a dataview.
')

(* ****** ****** *)

#declnamesynop("ArraySubscriptExn")
#decldescrpt('\

By convention, this exception is raised to indicate a situation where the
index involved in array subscripting is out-of-bounds.\

')

(* ****** ****** *)

#declnamesynop("lemma_array_param")
#decldescrpt('\
This proof function establishes that the integer n in any array-type
#stacode("@[T][n]") is a natural number.
')

(* ****** ****** *)

#declnamesynop("lemma_array_v_param")
#decldescrpt('\
This proof function establishes that the integer n in any array-view
#stacode("array_v(T, l, n)") is a natural number.
')

(* ****** ****** *)

#declnamesynop("array_v_nil")
#decldescrpt('\
This proof function generates a proof of empty array-view.
')

(* ****** ****** *)

#declnamesynop("array_v_unnil")
#decldescrpt('\
This proof function consumes a proof of empty array-view.
')

(* ****** ****** *)

#declnamesynop("array_v_unnil_nil")
#decldescrpt('\
This proof function consumes a proof of empty array-view and then generates
a proof of empty array-view. It is a combination of array_v_unnil and array_v_nil.
')

(* ****** ****** *)

#declnamesynop("array_v_cons")
#decldescrpt('\
This proof function composes a proof of at-view and
a proof of array-view of size n to form a proof of array-view of size n+1.
')

#declnamesynop("array_v_uncons")
#decldescrpt('\

This proof function is the inverse of #prfcode("array_v_cons"): It
decomposes a proof of array-view of size n into a proof of at-view and a
proof of array-view of size n-1, where n is positive.

')

(* ****** ****** *)

#declnamesynop("array_v_sing")
#decldescrpt('\
This proof function turns a proof of at-view into a proof of array-view of
size 1.
')

(* ****** ****** *)

#declnamesynop("array_v_unsing")
#decldescrpt('\

This proof function is the inverse of #prfcode("array_v_sing"): It turns a
proof of array-view of size 1 into a proof of at-view.

')

(* ****** ****** *)

#declnamesynop("array_getref_at")
#decldescrpt('\

This function returns the pointer to array-cell i of the given array A,
that is, the pointer equal to addr@(A)+i*#sizeof("a"), where addr@(A)
refers to the starting address of A.  ')

#declexample('\
A typical use of
#dyncode("array_getref_at") is given in the following example:
#pats2xhtmld_tryit('\
//
staload UN = "prelude/SATS/unsafe.sats"
//
fun{a:t0p}
array_get_at{n:int}
(
  A: &(@[a][n]), i: sizeLt n
) : a = let
  val p = array_getref_at<a> (A, i) in $UN.cptr_get (p)
end // end of [array_get_at]

fun{a:t0p}
array_set_at{n:int}
(
  A: &(@[a][n]), i: sizeLt n, x: a
) : void = let
  val p = array_getref_at<a> (A, i) in $UN.cptr_set (p, x)
end // end of [array_set_at]
')

Note that unsafe functions
#dyncode("cptr_get") and #dyncode("cptr_set")
are called here to read from and write through the pointer p.

')

(* ****** ****** *)

#declnamesynop("array_get_at")

(* ****** ****** *)

#declnamesynop("array_get_at_gint")
#decldescrpt('\

This function, which overloads the symbol #dyncode("[]"), returns the value
stored in array-cell i of the given array A.  Note that the type for values
stored in A is nonlinear.  ')

#declexample("\

The following code computes the sum of the doubles stored in a given array:

#pats2xhtmld_tryit('\
fn tally{n:nat}
(
  A: &(@[double][n]), n: int n
) :<(*none*)> double = let
//
fun loop
  {i:nat | i <= n} .<n-i>.
(
  A: &(@[double][n]), n: int n, i: int i, res: double
) :<(*none*)> double =
  if n > i then loop (A, n, i+1, res + A[i]) else res
//
in
  loop (A, n, 0(*i*), 0.0(*res*))
end // end of [tally]
')
")

(* ****** ****** *)

#declnamesynop("array_get_at_guint")
#decldescrpt('\

This function, which overloads the symbol #dyncode("[]"), is like
#dyncode("array_get_at_gint") except that the index is unsigned.  ')

(* ****** ****** *)

#declnamesynop("array_set_at")

(* ****** ****** *)

#declnamesynop("array_set_at_gint")
#decldescrpt("\

This function, which overloads the symbol #dyncode("[]"), stores a value
into array-cell i of the given array A, overwriting the original
value. Note that the type of values stored in A is nonlinear.  ")
#declexample("\

The following code doubles the integer value of each array-cell in a given
array:

#pats2xhtmld_tryit('\
fn doubling{n:nat}
(
  A: &(@[int][n]), n: int n
) :<!wrt> void = let
//
fun loop
  {i:nat | i <= n} .<n-i>.
(
  A: &(@[int][n]), n: int n, i: int i
) :<!wrt> void =
  if n > i then (A[i] := 2 * A[i]; loop (A, n, i+1)) else ()
//
in
  loop (A, n, 0)
end // end of [doubling]
')
")

(* ****** ****** *)

#declnamesynop("array_set_at_guint")
#decldescrpt('\

This function, which overloads the symbol #dyncode("[]"), is like
#dyncode("array_set_at_gint") except that the index is unsigned.  ')

(* ****** ****** *)

#declnamesynop("array_exch_at")

(* ****** ****** *)

#declnamesynop("array_exch_at_gint")
#decldescrpt('\
This function exchanges the value in array-cell i of the given array A and the
value in its second argument. Note that the type of these values can be linear.
')
#declnamesynop("array_exch_at_guint")
#decldescrpt('\

This function, which is like #dyncode("array_exch_at_gint") except that the
index is unsigned.  ')

(* ****** ****** *)

#declnamesynop("array_interchange")
#decldescrpt('\

Given an array and two valid indices i and j, this function interchanges
the values in array-cell i and array-cell j.  Note that the type of these
values can be linear.  ')

#declexample("\

The following code implements the standard insertion sort:
#pats2xhtmld_tryit('\
fn{a:t0p}
insertion_sort
  {n:int} (
  A: &(@[a][n]), n: size_t n
) :<!wrt> void = let
//
fun loop1
  {i:nat | i < n} .<i>.
(
  A: &(@[a][n]), i: size_t i
) :<!wrt> void =
  if i > 0 then let
    val i1 = pred (i)
    val sgn = gcompare_ref<a> (A.[i1], A.[i])
  in
    if sgn > 0 then
    (
      array_interchange (A, i1, i); loop1 (A, i1)
    ) // end of [if]
  end else () // end of [if]
//
fun loop2
  {i:nat | i <= n} .<n-i>.
(
  A: &(@[a][n]), n: size_t n, i: size_t i
) :<!wrt> void =
  if i < n then let
    val () = loop1 (A, i) in loop2 (A, n, succ(i))
  end else () // end of [if]
//
in
//
if n >= 2 then loop2 (A, n, g1i2u(1)) else ()
//
end // end of [insertion_sort]
')
")

(* ****** ****** *)

#declnamesynop("array_subcirculate")
#decldescrpt('\

Given an array A and two valid indices i and j, this function circulates
the values stored in array-cells between cell i and cell j, inclusive.  If
i is less than j, then A[k] moves into A[k+1] for k ranging from i up to
j-1 and A[j] moves into A[i]. If i is greater than j, then A[k] moves into
A[k-1] for k ranging from i down to j+1, and A[j] moves into A[i].\

')

(* ****** ****** *)

#declnamesynop("array_ptr_alloc")

(* ****** ****** *)

#declnamesynop("array_ptr_free")

(* ****** ****** *)

#declnamesynop("array_ptr_tabulate")
#declnamesynop("array_tabulate$fopr")

(* ****** ****** *)

#declnamesynop("fprint_array")
#decldescrpt("\

This function prints the elements in a given array to the output
channel provided as its first argument, and it calls the function
#dyncode("fprint_array$sep") before printing an element as long as the
element is not the first one.  Note that printing each array element is
handled by calling the function #dyncode("fprint_ref").  ")

#declnamesynop("fprint_array$sep")

(* ****** ****** *)

#declnamesynop("fprint_array_sep")
#decldescrpt("\

This function prints the elements in a given array to the output channel
provided as its first argument, interspersing the string #dyncode('sep')
between the printed array elements. Note that printing each array element is
handled by calling the function #dyncode("fprint_ref").

")

(* ****** ****** *)

#declnamesynop("array_foreach")
#decldescrpt('\

This function traverses a given array from left to right, applying to each
encountered array-cell the function implemented by
#dyncode("array_foreach$fwork").  The traversal stops if the function
implemented by #dyncode("array_foreach$cont") returns false, and the return
value of #dyncode("array_foreach") indicates the number of processed
array-cells.  ')
#declexample('\

The following code implements a function that searches for the index of the
first array-cell in a given array that contains an element satisfing the
predicate provided as the third argument of the function:

#pats2xhtmld_tryit('\
fun{a:t@ype}
array_find{n:int}
(
  A: &(@[a][n]) >> @[a][n], n: size_t n, p: (a) -> bool
) : Option_vt (sizeLt(n)) = let
//
implement(env)
array_foreach$cont<a><env> (x, env) = ~p(x)
implement(env)
array_foreach$fwork<a><env> (x, env) = ((*nothing*))
val i = array_foreach (A, n)
//
in
  if n > i then Some_vt (i) else None_vt ()
end // end of [array_find]
')
')

(* ****** ****** *)

#declnamesynop("array_foreach_env")
#decldescrpt('\

This function does essentially the same as #dyncode("array_foreach")
except for taking an additional argument that serves as an environment.\

')

#declexample('\

The following code implements the standard fold-from-left operation on a
given array:

#pats2xhtmld_tryit('\
fun{
a:t0p}{res:t0p
} array_foldleft{n:int}
(
  A: &(@[a][n])
, asz: size_t n, ini: res, f: (res, a) -> res
) : res = let
//
var env: res = ini
//
implement
array_foreach$fwork<a><res> (x, env) = env := f (env, x)
//
val _(*asz*) = array_foreach_env (A, asz, env)
//
in
  env
end // end of [array_foldleft]
')
')

(* ****** ****** *)

#declnamesynop("array_foreach$cont")
#decldescrpt('\
The default implementation of this function always returns true.\
')

#declnamesynop("array_foreach$fwork")

(* ****** ****** *)

#declnamesynop("array_foreach_funenv")
#decldescrpt('\

This function is like #dyncode("array_foreach") except that the function
to be applied to each array-cell is provided as an argument.  ')

#declnamesynop("array_foreach_fun")
#decldescrpt('\

This function is a special case of #dyncode("array_foreach_funenv") in that
the provided function does not take an argument as its environment.  ')

(* ****** ****** *)

#declnamesynop("array_foreach2")

#declnamesynop("array_foreach2_env")

#declnamesynop("array_foreach2$cont")
#decldescrpt('\
The default implementation of this function always returns true.\
')

#declnamesynop("array_foreach2$fwork")

(* ****** ****** *)

#declnamesynop("array_rforeach")
#decldescrpt('\

This function traverses a given array from right to left, applying to each
encountered array-cell the function implemented by
#dyncode("array_rforeach$fwork"). The traversal stops is the function
implemented by #dyncode("array_rforeach$cont") returns false, and the
return value of #dyncode("array_rforeach") indicates the number of
processed array-cells.  ')
#declexample('\

The following code implements a function that searches for the index of the
last array-cell in a given array that contains an element satisfing the
predicate provided as the third argument of the function:

#pats2xhtmld_tryit('\
fun{a:t@ype}
array_rfind{n:int}
(
  A: &(@[a][n]) >> @[a][n], n: size_t n, p: (a) -> bool
) : Option_vt (sizeLt(n)) = let
//
implement(env)
array_rforeach$cont<a><env> (x, env) = ~p(x)
implement(env)
array_rforeach$fwork<a><env> (x, env) = ((*nothing*))
val i = array_rforeach (A, n)
//
in
  if n > i then Some_vt (pred(n)-i) else None_vt ()
end // end of [array_rfind]
')
')

#declnamesynop("array_rforeach_env")
#decldescrpt('\

This function does essentially the same as #dyncode("array_rforeach")
except for taking an additional argument that serves as an environment.  ')

#declexample('\

The following code implements the standard fold-from-right operation on a
given array:

#pats2xhtmld_tryit('
fun{
a:t0p}{res:t0p
} array_foldright{n:int}
(
  A: &(@[a][n])
, asz: size_t n, f: (a, res) -> res, snk: res
) : res = let
//
var env: res = snk
//
implement
array_rforeach$fwork<a><res>
  (x, env) = env := f (x, env)
//
val _(*asz*) = array_rforeach_env<a><env> (A, asz, env)
//
in
  env
end // end of [array_foldright]
')

Please note that #dyncode("array_rforeach_env") is given a
tail-recursive implementation in ATSLIB.\

')

(* ****** ****** *)

#declnamesynop("array_rforeach$cont")
#decldescrpt('\
The default implementation of this function always returns true.\
')

#declnamesynop("array_rforeach$fwork")

(* ****** ****** *)

#declnamesynop("array_initize")
#declnamesynop("array_initize$init")

(* ****** ****** *)

#declnamesynop("array_initize_elt")

(* ****** ****** *)

#declnamesynop("array_initize_list")
#declnamesynop("array_initize_rlist")

(* ****** ****** *)

#declnamesynop("array_initize_list_vt")
#declnamesynop("array_initize_rlist_vt")

(* ****** ****** *)

#declnamesynop("array_uninitize")
#declnamesynop("array_uninitize$clear")

(* ****** ****** *)

#declnamesynop("array_bsearch")
#declnamesynop("array_bsearch$ford")

(* ****** ****** *)

#declnamesynop("array_bsearch_stdlib")
#decldescrpt("\

This function is a wrapper around the function #dyncode("bsearch") declared
in stdlib.h of libc.\

")

(* ****** ****** *)

#declnamesynop("array_quicksort")

#declnamesynop("array_quicksort$cmp")
#decldescrpt("\

This function is called in the implementation of
#dyncode("array_quicksort") to perform comparison test on array elements,
and its default implementation is based on #dyncode("gcompare_ref"):

#pats2xhtmld_tryit("\
implement{a}
array_quicksort$cmp (x, y) = gcompare_ref<a> (x, y)
")
")

(* ****** ****** *)

#declnamesynop("array_quicksort_stdlib")
#decldescrpt("\

This function is a wrapper around the function #dyncode("qsort") declared
in stdlib.h of libc.\

")

(* ****** ****** *)

#declnamesynop("array_mapto")
#declnamesynop("array_mapto$fwork")

(* ****** ****** *)

#declnamesynop("array_map2to")
#declnamesynop("array_map2to$fwork")

(* ****** ****** *)

#declnamesynop("array_permute")
#decldescrpt('\

This function permutes the contents of a given array according to the
random number generator implemented by #dyncode("array_permute$randint").
')
#declexample('\

The following code creates an array of integers 1, 2, 3, 4, and 5, and then
permutes the array contents to obtain an array of integers 5, 1, 2, 3, and
4:

#pats2xhtmld_tryit('\
implement
main0 () = {
//
\#define N 5
val asz = g1i2u (N)
val out = stdout_ref
//
val (pf, pfgc | p) = array_ptr_alloc<int> (asz)
//
implement
array_initize$init<int> (i, x) = x := g0u2i(i)+1
val () = array_initize<int> (!p, asz) // array: 1, 2, ..., N-1, N
//
val (
) = fprint_array_sep (out, !p, asz, ",")
val () = fprint_newline (out)
//
implement
array_permute$randint<> (n) = pred(n) // this is not random
val () = array_permute<int> (!p, asz) // array: N, 1, 2, ..., N-1
//
val (
) = fprint_array_sep (out, !p, asz, ",")
val () = fprint_newline (out)
//
val () = array_ptr_free (pf, pfgc | p)
//
} // end of [main0]
')\

Note that the created array is properly freed before the code exits.  ')

(* ****** ****** *)

#declnamesynop("array_permute$randint")

(* ****** ****** *)
//
// HX: various overloaded symbols
//
(* ****** ****** *)

#declname("Overloaded Symbols")

(* ****** ****** *)

#declnamesynop("[]")

(* ****** ****** *)

(* end of [array.atxt] *)
