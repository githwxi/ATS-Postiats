(* ****** ****** *)

%{
implement srcfilename_get () = "../string.sats"
%}

(* ****** ****** *)

%{
val () = patscode_count_reset()
val () = patscode_prefix_set("TRYIT/prelude_string")
%}

(* ****** ****** *)

#decltitle("\
ATSLIB/prelude/string\
")

(* ****** ****** *)

#declpreamble('\
#para("\

This package contains various common functions for processing (immutable)
strings.

") ')#comment("declpreamble")

(* ****** ****** *)

#declnamesynop("string_index_p")
#decldescrpt('\

Given a string str of length n and an index i, #stacode("string_index_p(n,
i, c)") means that str[i] in the string is of the type #stacode("char(c)").
If str[i] equals the null character, then i equals n.\

')

(* ****** ****** *)

#declnamesynop("lemma_string_param")

(* ****** ****** *)

#declnamesynop("string2ptr")

(* ****** ****** *)

#declnamesynop("g0ofg1_string")
#declnamesynop("g1ofg0_string")

(* ****** ****** *)

#declnamesynop("string_sing")
#decldescrpt('\
This function constructs a singleton string consisting of its char argument.
')
#declfunretval("A linear string of length 1 is returned.")

(* ****** ****** *)

#declnamesynop("string_make_list")
#decldescrpt('\

This function returns a linear string consisting of the same sequence of
non-null chars in its argument.  ')

#declfunretval("A linear string is returned.")

#declexample('\

The following code implements a function that returns a linear string
consisting of the reverse of the sequence of chars in its argument:

#pats2xhtmld_tryit('\
staload UN = "prelude/SATS/unsafe.sats"
//
fun
string_reverse
  {n:int} .<>.
(
  str: string(n)
) : strnptr(n) = let
//
prval () = lemma_string_param (str)
//
val cs = string_explode (str)
val cs = list_vt_reverse (cs)
val res = string_make_list ($UN.list_vt2t(cs))
val ((*void*)) = list_vt_free (cs)
//
in
  res
end // end of [string_reverse]
')
')

(* ****** ****** *)

#declnamesynop("string_make_listlen")
#decldescrpt('\

This function does the same as #dyncode("string_make_list") except for
taking the length of the given string as its second argument.  ')

(* ****** ****** *)

#declnamesynop("string_make_rlist")
#decldescrpt('\

This function returns a linear string consisting of the reverse of the
sequence of non-null chars in its argument.  ')

#declfunretval("A linear string is returned.")

(* ****** ****** *)

#declnamesynop("string_make_rlistlen")
#decldescrpt('\

This function does the same as #dyncode("string_make_rlist") except for
taking the length of the given string as its second argument.  ')

#declfunretval("A linear string is returned.")

(* ****** ****** *)

#declnamesynop("string_make_substring")

#decldescrpt("\

Given a string str of length n and integers st and ln, this function
returns a string that equals the substring in str that starts at char st
and is of length ln.\

")
#declfunretval("A linear string is returned.")

(* ****** ****** *)

#declnamesynop("string_is_empty")
#decldescrpt('\
This funtion returns true if and only if its string argument is empty.
')
#declfunretval("A boolean value is returned.")

#declnamesynop("string_isnot_empty")
#decldescrpt('\
This funtion returns true if and only if its string argument is non-empty.
')
#declfunretval("A boolean value is returned.")

(* ****** ****** *)

#declnamesynop("string_is_atend_gint")
#decldescrpt('\

Given a string of length n and an index i &lt;= n, this function returns true
if and only if i equals n.  ')

#declfunretval("A boolean value is returned.")

#declnamesynop("string_is_atend_guint")
#decldescrpt('\

This function does the same as #dyncode("string_is_atend_gint") except for
taking an unsigned integer as the string index.  ')

#declfunretval("A boolean value is returned.")

(* ****** ****** *)

#declnamesynop("string_get_at")

(* ****** ****** *)

#declnamesynop("string_get_at_gint")
#decldescrpt('\

This function, which overloads the symbol #dyncode("[]"), returns the char
stored in cell i of the given string.  Note that the given index i is
guranteed to be legal and the returned character is always non-null. ')

#declfunretval("A non-null char is returned.")

#declnamesynop("string_get_at_guint")
#decldescrpt('\

This function, which overloads the symbol #dyncode("[]"), does the same as
#dyncode("string_get_at_gint") except for taking an unsigned integer as the
string index.  ')

#declfunretval("A non-null char is returned.")

(* ****** ****** *)

#declnamesynop("string_test_at")

(* ****** ****** *)

#declnamesynop("string_test_at_gint")
#decldescrpt('\

Given a string of length n and an index i &lt;= n, this function returns a
char c paired with a proof stating that i &lt; n holds if and only if c is
non-null. ')

#declfunretval("A char is returned.")

#declexample('\

The following code implements a function for computing the length of a
given string:

#pats2xhtmld_tryit('\
fun
string1_length
  {n:int} .<>.
  (str: string n): size_t(n) = let
//
\#define NUL \'\\\000\'
//
prval () = lemma_string_param (str)
//
fun loop
  {i:nat | i <= n} .<n-i>.
(
  str: string (n), i: size_t (i)
) :<> size_t (n) = let
  val (pf | c) = string_test_at (str, i)
in
  if c != NUL then let
    prval
    string_index_p_neqz () = pf in loop (str, succ(i))
  end else let
    prval string_index_p_eqz () = pf in i // HX: i equals n
  end // end of [if]
end // end of [loop]
//
in
  loop (str, g1i2u(0))
end // end of [string1_length]
')
')

#declnamesynop("string_test_at_guint")
#decldescrpt('\

This function does the same as #dyncode("string_test_at_gint") except for
taking an unsigned integer as the string index.  ')

#declfunretval("A char is returned.")

(* ****** ****** *)

#declnamesynop("lt_string_string")
#decldescrpt('\

This function returns true if and only if its first argument is less than
its second argument according to the lexicographic ordering on signed
characters.  ')

#declnamesynop("lte_string_string")
#decldescrpt('\

This function returns true if and only if its first argument is less than
or equal to its second argument according to the lexicographic ordering on
signed characters.  ')

(* ****** ****** *)

#declnamesynop("gt_string_string")

#decldescrpt('\

This function returns true if and only if its first argument is greater
than its second argument according to the lexicographic ordering on
signed characters.  ')

#declnamesynop("gte_string_string")

#decldescrpt('\

This function returns true if and only if its first argument is greater
than or equal to its second argument according to the lexicographic
ordering on signed characters.  ')

(* ****** ****** *)

#declnamesynop("eq_string_string")

#decldescrpt('\

This function returns true if and only if its first argument is equal to
its second argument according to the lexicographic ordering on
signed characters. ')

#declnamesynop("neq_string_string")
#decldescrpt('\

This function returns true if and only if its first argument is unequal to
its second argument according to the lexicographic ordering on
signed characters. ')

(* ****** ****** *)

#declnamesynop("compare_string_string")
#decldescrpt('\

This function returns ~1, 0 and 1 if and only if its first argument is less
than, equal to and greater than its second argument according to the
lexicographic ordering on signed characters. ')

')
#declfunretval("An integer between ~1 (negative 1) and 1 is returned.")

(* ****** ****** *)

#declnamesynop("strcmp")
#decldescrpt('\

This function returns a negative integer, 0 and a postive integer to
indicate that its first string argument is less than, equal to and greater
than its second string argument, respectively, where the comparison is
based on the lexicographic ordering on signed chars.  ')

#declnamesynop("strintcmp")
//
#decldescrpt('\

This function returns ~1,0 and 1 to indicate that the length of its first
string argument is less than, equal to and greater than the value of its
second integer argument, respectively.  ')
//
#declfunretval("An integer between ~1 (negative 1) and 1 is returned.")

#declnamesynop("strlencmp")
//
#decldescrpt('\

This function returns -1, 0 and 1 to indicate that the length of its first
string argument is less than, equal to and greater than the length of its
second string argument, respectively.\

')
//
#declfunretval("An integer between ~1 (negative 1) and 1 is returned.")

(* ****** ****** *)

#declnamesynop("fprint_string")
#decldescrpt('\

This function prints a string (its second argument) to a given output
channel (its first argument). The implementation of this function calls the
libc-function fputs declared in [stdio.h]. ')

(* ****** ****** *)

#declnamesynop("strchr")
#decldescrpt('\

Given a string str and a char c0, this function returns the index of the
first char in str that equals c0 if there is such a char.  Otherwise, it
returns ~1 (negative 1).  The implementation of this function in ATSLIB
calls the libc-function of the same name (declared in [string.h]).
')

#declnamesynop("strrchr")
#decldescrpt('\

Given a string str and a char c0, this function returns the index of the
last char in str that equals c0 if there is such a char.  Otherwise, it
returns ~1 (negative 1).  The implementation of this function in ATSLIB
calls the libc-function of the same name (declared in [string.h]).  ')

#declnamesynop("strstr")
#decldescrpt('\

Given a string str1 and a string str2, this function returns the index of
the first substring in str1 that equals str2 if there is such a
substring. Otherwise, it returns ~1 (negative 1).  Note that the function
returns 0 in the special case where str2 is empty, The implementation of
this function in ATSLIB calls the libc-function of the same name (declared
in [string.h]).  ')

(* ****** ****** *)

#declnamesynop("strspn")
#declnamesynop("strcspn")

(* ****** ****** *)

#declnamesynop("string_index")
#decldescrpt('\

This function is the same as #dyncode("strchr") except for requiring that
its second argument be a non-null char.  The implementation of this
function in ATSLIB calls the libc-function of the same name (declared in
[strings.h]).  ')

#declnamesynop("string_rindex")
#decldescrpt('\

This function is the same as #dyncode("strrchr") except for requiring that
its second argument be a non-null char.  The implementation of this
function in ATSLIB calls the libc-function of the same name (declared in
[strings.h]).  ')

(* ****** ****** *)

#declnamesynop("string0_length")
#decldescrpt('\

This function, which overloads the symbol #dyncode("string_length"), computes
the length of a given string.  ')

#declnamesynop("string1_length")
#decldescrpt('\

This function, which overloads the symbol #dyncode("string_length"), does the
same as #dyncode("string0_length") but is assigned a more informative type.
')

(* ****** ****** *)

#declnamesynop("string0_copy")
#declnamesynop("string1_copy")

#declnamesynop("string0_append")
#decldescrpt('\

This function, which overloads the symbol #dyncode("string_append"),
returns a linear string equal to the concatenation of its two string
arguments. ')

#declnamesynop("string1_append")
#decldescrpt('\

This function, which overloads the symbol #dyncode("string_append"), does
the same as #dyncode("string0_append") but is assigned a more informative
type.  ')

(* ****** ****** *)

#declnamesynop("stringlst_concat")
#decldescrpt("\

This function returns a linear string that is the concatenation of the
strings in its argument. It is implemented based on
#dyncode("stringarr_concat").\

")

#declexample('\

The following code builds a linear string "Hello" by concatenating five
singleton strings and then prints it (plus a newline) onto the standard
output channel:

#pats2xhtmld_tryit('\
implement
main0 () =
{
//
val xs =
$list{string}("H", "e", "l", "l", "o")
//
val Hello = stringlst_concat (xs)
val () = fprintln! (stdout_ref, Hello)
val () = strptr_free (Hello)
//
} (* end of [main] *)
')\

Note that the created linear string is freed before the code exits.

')

(* ****** ****** *)

#declnamesynop("stringarr_concat")
#decldescrpt("\

This function returns a linear string that is the concatenation of the
strings in its argument.  ")

(* ****** ****** *)

#declnamesynop("string_explode")
#decldescrpt('\

Given a string of length n, this function returns a linear list
such that element i in the list equals char i in the string for
0 &lt;= i &lt; n.

')

(* ****** ****** *)

#declnamesynop("string_tabulate")
//
#decldescrpt('\

Given an integer n, this function returns a string of length n such that
char i in the string equals f(i) for 0 &lt;= i &lt n, where f is the
function implemented by #dyncode("string_tabulate$fwork"). ')
//
#declfunretval("A linear string is returned.")
//
#declexample('\

The following code gives an implementation of
#dyncode("string_make_substring"), which constructs a linear string
consisting of the chars in a segment of a given string:

#pats2xhtmld_tryit('\
//
staload UN = "prelude/SATS/unsafe.sats"
//
implement
string_make_substring
  {n}{st,ln} (str, st, ln) = let
//
implement
string_tabulate$fwork (i) = let
  val i = $UN.cast{sizeLt(ln)} (i) in str[st+i]
end // end of [string_tabulate$fwork]
//
in
  $effmask_all (string_tabulate (ln))
end // end of [string_make_substring]
')
')

#declnamesynop("string_tabulate$fopr")

(* ****** ****** *)

#declnamesynop("string_foreach")
//
#decldescrpt('\
This function traverses a given string from left to right and applies to
each encountered char the function implemented by
#dyncode("string_foreach$fwork"). The traversal stops if the function
implemented by #dyncode("string_foreach$cont") returns false.  ')
//
#declparamadd("#dyncode("str")", "the string to be traversed.")
#declparamlist()
#declfunretval("The number of processed chars is returned.")
//
#declexample('\

The following code implements the function #dyncode("strspn") that computes
the length of the longest prefix of a given string consisting of only chars
contained in the second argument of this function:

#pats2xhtmld_tryit('\
implement
strspn (str, accept) = let
//
val accept = string1_of_string0 (accept)
//
implement{env}
string_foreach$cont (c, env) = strchr (accept, c) >= 0
//
in
  $effmask_all (string_foreach<> (str))
end // end of [strspn]
')
')

(* ****** ****** *)

#declnamesynop("string_foreach_env")
//
#decldescrpt('\

This function does essentially the same as #dyncode("string_foreach")
except for taking an additional argument that serves as an environment.  ')
//
#declfunretval("The number of processed chars is returned.")
//
#declexample('\
The following code implements a function that computes the value of an
unsigned integer represented as a sequence of digits (of base 10):

#pats2xhtmld_tryit('\
fun atoi
  (str: string): int = let
//
val str = string1_of_string0 (str)
//
var env: int = 0
implement
string_foreach$fwork<int>
  (c, env) = env := 10 * env + (c - \'0\')
val _ = string_foreach_env<int> (str, env)
//
in
  env
end // end of [atoi]
')
Note that this function assumes its string argument consisting of
a sequence of digits.
')

(* ****** ****** *)

#declnamesynop("string_foreach$cont")
#declnamesynop("string_foreach$fwork")

(* ****** ****** *)

#declnamesynop("string_rforeach")
//
#decldescrpt('\

This function traverses a given string from right to left and applies to
each encountered char the function implemented by
#dyncode("string_rforeach$fwork"). The traversal stops if the function
implemented by #dyncode("string_rforeach$cont") returns false.

')
//
#declfunretval("The number of processed chars is returned.")
//
#declexample('\

The following code implements the function #dyncode("strrchr") that finds
the index of the last occurrence of a char in a given string. If there is
no such occurrence, then ~1 (negative 1) is returned.

#pats2xhtmld_tryit('\
implement{}
strrchr (str, c0) = let
//
implement{env}
string_rforeach$cont (c, env) = c0 != c
val i = $effmask_all (string_rforeach<> (str))
//
in
  if string_is_atend (str, i) then g1i2i(~1) else g1u2i(i)
end // end of [strrchr]
')
')

(* ****** ****** *)

#declnamesynop("string_rforeach_env")
//
#decldescrpt('\

This function does essentially the same as #dyncode("string_rforeach")
except for taking an additional argument that serves as an environment.  ')
//
#declfunretval("The number of processed chars is returned.")

(* ****** ****** *)

#declnamesynop("string_rforeach$cont")
#declnamesynop("string_rforeach$fwork")

(* ****** ****** *)

#declnamesynop("stropt_none")
#declfunretval("The null pointer is returned as a stropt-value.")

#declnamesynop("stropt_some")
#decldescrpt("This function casts a string into a stropt-value.")

(* ****** ****** *)

#declnamesynop("stropt_is_none")
#decldescrpt('\

This function tests whether a stropt-value (optional string) is null.  ')

#declfunretval("A boolean value is returned.")

#declnamesynop("stropt_is_some")
#decldescrpt('\

This function tests whether a stropt-value (optional string) is non-null.  ')

#declfunretval("A boolean value is returned.")

(* ****** ****** *)

#declnamesynop("stropt_unsome")
#decldescrpt('\

This function casts a non-null stropt-value (optional string) to a
string-value.  ')

#declfunretval("A string is returned.")

(* ****** ****** *)

#declnamesynop("stropt_length")
#decldescrpt('\

This function computes the length of a given stropt-value (optional
string). If the argument is the null pointer, then the returned value is ~1
(negative 1).\

')
#declfunretval('\
A signed-size integer is returned.
')

(* ****** ****** *)

#declnamesynop("fprint_stropt")
//
#decldescrpt('\
This function prints a stropt-value (optional string) to a given output
channel (its first argument). If the stropt-value is not null, then it is
treated as a string. Otherwise, it is treated as the null pointer. ')
//
#declparamadd("#dyncode("out")", "the channel for receiving the output.")
//
#declparamadd("#dyncode("x")", "\
the optional string to be printed. Note that an optional string is either a
string or null pointer.")
//
#declparamlist()
//
#declfunretval("The void value is returned.")

(* ****** ****** *)
//
// HX: various overloaded symbols
//
(* ****** ****** *)

#declname("Overloaded Symbols")

(* ****** ****** *)

#declnamesynop("g0ofg1")
#declnamesynop("g1ofg0")

(* ****** ****** *)

#declnamesynop("<")
#declnamesynop("<=")
#declnamesynop(">")
#declnamesynop(">=")
#declnamesynop("=")
#declnamesynop("!=")
#declnamesynop("compare")

(* ****** ****** *)

#declnamesynop("string_is_atend")

(* ****** ****** *)

#declnamesynop("length")
#declnamesynop("string_length")

(* ****** ****** *)

#declnamesynop("[]")

(* ****** ****** *)

#declnamesynop("string_append")

(* ****** ****** *)

#declnamesynop("print")
#declnamesynop("prerr")
#declnamesynop("fprint")

(* ****** ****** *)

(* end of [string.atxt] *)
